* CREATEA
* CREATE.ACCOUNT verb
* Copyright (c) 2007 Ladybridge Systems, All Rights Reserved
*
* This program is free software; you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation; either version 3, or (at your option)
* any later version.
* 
* This program is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU General Public License for more details.
* 
* You should have received a copy of the GNU General Public License
* along with this program; if not, write to the Free Software Foundation,
* Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
* 
* 
* START-HISTORY:
* rev 1.0-1 Jan 26 group account now have linux group name written as lower case
*           with "sdg_" prefix
*           use "adduser" -p parameter when creating linux user account
* rev 0.9-3 Nov 25 move voc back to dynamic file
* rev 0.9.0 Jan 25 mab change dyn file prefix to %
*           sd group belong to linux group of the same name
*           on sd directory creation, we set the setgid bit
*           sd user's sd group is named sdu_<username>
*           add %t  ~ to mapping sub 
*           add ability to create os user
* 20240330 mab rev 064 list of valid users required in ACCOUNTS<2>
* 20240309 mab create file BP for new accounts
* 20240219 mab dflt user and group parent directory for accounts
*
* mods to command line:
*  Rem was
*    CREATE.ACCOUNT acc.name pathname {NO.QUERY}
*  Now 
*    CREATE.ACCOUNT USER <user acct name>  {NO.QUERY}  - account created in  /home/sd/user_accounts/<user acct name>  owner:group set to <username>:"sdu_"+<username>
*                                                 <username> must be found in sdusers group!
*    CREATE.ACCOUNT GROUP <grp acct name>  {NO.QUERY}  - account created in  /home/sd/group_accounts/<grp acct name> owner:group set to sdsys:group_name
*                                                        Note - users will need to be added to group with MODIFY.ACCOUNT ADD group_name username
*    CREATE.ACCOUNT OTHER acc.name pathname {NO.QUERY} - account created in pathname, maintained for backwards compatiblilty. This will require manual editing
*                                                        of the accounts record to specify linux group the account belongs to.
*
* 19 Jan 04  0.6.1 SD launch. Earlier history details suppressed.
* END-HISTORY
*
* START-DESCRIPTION:
*
* CREATE.ACCOUNT account.name  path.name [NO.QUERY]
*
* END-DESCRIPTION
*
* START-CODE

$internal
program $create.account
$catalog $CREATEA

$include keys.h
$include int$keys.h
$include parser.h
$include err.h
* 20240219 mab user must be member of sdusers  
deffun is_grp_member(user,group) calling '!is_grp_member'
* rev 0.9.0 *
deffun is_group(group) calling '!is_group'
deffun is_user(uname) calling '!is_user'
deffun create_user(uname) calling '!create_user'
deffun set_passwd(uname)  calling '!set_passwd'

   prompt ''

* rev 0.9.0 create-account requires admin priv, are we admin?
   if not(kernel(K$ADMINISTRATOR, -1)) then
      stop sysmsg(2001)  ;* Command requires administrator privileges
   end
      
* we run as root, and the root umask is usually 022, that will not work for us
   if umask(002) then null

   parser = "!PARSER"
   no.query = @false

   @system.return.code = -ER$ARGS     ;* Preset for argument errors


   openpath @sdsys:@ds:'ACCOUNTS' to acc.f else
      @system.return.code = -status()
      stop sysmsg(6000) ;* Cannot open ACCOUNTS file in SDSYS account
   end


   call @parser(PARSER$RESET, 0, @sentence, 0)
   call @parser(PARSER$GET.TOKEN, token.type, token, keyword) ;* Verb create-account
   * 20240219 mab dflt user and group parent directory for accounts
   * Get next token, could be USER, GROUP or account name

   call @parser(PARSER$GET.TOKEN, token.type, acc.type, keyword)
   *
   uid = ''
   group_name = ''   ;* will be set to callers entered group name on group create, no case changes
   begin case
   case upcase(acc.type) = 'USER' 
*    CREATE.ACCOUNT USER <user acct name>  {NO.QUERY}   
     pathname = CONFIG('USRDIR')
     if pathname = '' then pathname = '/home/sd/user_accounts'
     call @parser(PARSER$GET.TOKEN, token.type, acc.uname, keyword)
     if token.type = PARSER$END then stop
     * any other args? (specifically no.query)
     * rem we may abort in this routine
     gosub more.args
     
     * Is the user a valid os user?
     * Check reserved names then
     * Check user name as entered, then force to lower case and re check
     begin case
     
     * is user reserved?
     case upcase(acc.uname) = 'ROOT' 
       @system.return.code = -ER$ARGS
       stop sysmsg(6052) ;* User name is invalid
     
     case upcase(acc.uname) = 'SDSYS' 
       @system.return.code = -ER$ARGS
       stop sysmsg(6052) ;* User name is invalid
      
     case upcase(acc.uname) = 'SDUSERS' 
       @system.return.code = -ER$ARGS
       stop sysmsg(6052) ;* User name is invalid
          
     case is_user(acc.uname)
       null ;* user name as entered is valid
  
     case is_user(downcase(acc.uname))
       * lower is a valid user, use it
       acc.uname = downcase(acc.uname)
 
     case no.query
       * we don't have a valid user name and option no query specified, this will not work!
         @system.return.code = -ER$ARGS
         stop sysmsg(6074)  ;*    Invalid user name 
       
     case 1
       * not a valid os user, are we configured to auto create os user?
       If config('CREATUSR') then
       * yes, attempt to create os user
       * sd convention is os user names are lowercase, if you don't want this, pre create the os user
         acc.uname = downcase(acc.uname)
		 * Check INVERT/NOINVERT STATUS (1=INVERT on)
         PVAL = SYSTEM(1001)
         IF PVAL = 1 THEN
           CRT "Warning: **** Case Inversion is not active during password entry! ****"   
         END
         if not(create_user(acc.uname)) then
           * create user failed
           @system.return.code = ER$NOT.CREATED
           stop sysmsg(10006,STATUS()) ;* Create User Failed, OS Error: %1
         end else
           crt sysmsg(10007,acc.uname)  ;* 'User ':acc.uname:' Created' 
           * user created set passowrd
PW.TRY.AGAIN:           
           if not(set_passwd(acc.uname)) then
           *  password failed
              crt sysmsg(10008): ;* 'Warning, user created but password not set, Retry (Y/N)'
              input yn
              if upcase(yn) = 'Y' then goto PW.TRY.AGAIN
           end
         end
         
       end else
       * not configured to create user, error out
         @system.return.code = -ER$ARGS
         stop sysmsg(6074)  ;*    Invalid user name 
       end
     end case
     
     
     * save uid and gid for set.owner of USER account
     uid = acc.uname  
     gid = "sdu_":acc.uname
     pathname := @ds:acc.uname
     acc.name = acc.uname
     

   case upcase(acc.type) = 'GROUP' 
*    CREATE.ACCOUNT GROUP <grp acct name>  {NO.QUERY}     
     pathname = CONFIG('GRPDIR')
     if pathname = '' then pathname = '/home/sd/group_accounts'
     call @parser(PARSER$GET.TOKEN, token.type, acc.name, keyword)
     if token.type = PARSER$END then stop
     pathname := @ds:acc.name
     if upcase(acc.name) = 'SDUSERS' then
       @system.return.code = -ER$ARGS
       stop sysmsg(2202) ;* Account name is invalid
     end        
     * any other args? (specifically no.query)
     * rem we may abort in this routine
     gosub more.args
       
*
   case upcase(acc.type) = 'OTHER'  
*  CREATE.ACCOUNT OTHER <acc name> <path> {NO.QUERY} 
     * get account name
     call @parser(PARSER$GET.TOKEN, token.type, acc.name, keyword)
     if token.type = PARSER$END then stop
     if upcase(acc.name) = 'SDUSERS' then
       @system.return.code = -ER$ARGS
       stop sysmsg(2202) ;* Account name is invalid
     end 
     * Get pathname
     call @parser(parser$get.token, token.type, pathname, keyword)
     if token.type = PARSER$END then stop
     pathname = trim(pathname)
     * any other args? (specifically no.query)
     * rem we may abort in this routine
     gosub more.args
*
   case 1
*   invalid command arg
* rev 0.9.0 
    crt 'Command Syntax: CREATE.ACCOUNT USER | GROUP <user name | group name>  {NO.QUERY}'
    @system.return.code = -ER$ARGS
    stop 
   end case

   * Check if already exists in ACCOUNTS file.  We will need to do this
   * again when we are ready to create the account but doing it here
   * saves the user typing the path name before we spot the error.

   read acc.rec from acc.f, acc.name then      ;* 0275
      @system.return.code = -ER$ACC.EXISTS
      stop sysmsg(6002) ;* Account already exists
   end

   * Validate account name

   acc.name = upcase(acc.name)

   if len(acc.name) > 32 or not(alpha(acc.name[1,1])) or index(acc.name, ' ', 1) then
      stop sysmsg(6003) ;* Account name is invalid
   end

   * Validate pathname

   if not(ospath(pathname, OS$PATHNAME)) then
      stop sysmsg(6005) ;* Invalid account pathname
   end


   * Check if already exists in ACCOUNTS file

   readu acc.rec from acc.f, acc.name locked
      stop sysmsg(6006, status()) ;* ACCOUNTS record is locked by user xx
   end then
      @system.return.code = -ER$ACC.EXISTS
      stop sysmsg(6002) ;* Account already exists
   end

   * Check parent directory exists


   if index(pathname, @ds, 1) then
      parent.dir = field(pathname, @ds, 1, dcount(pathname, @ds) - 1)
      if parent.dir # '' and not(parent.dir matches "1A':") then
         if not(ospath(parent.dir, OS$EXISTS)) then
            @system.return.code = -ER$NO.DIR
            stop sysmsg(6008) ;* Parent directory does not exist
         end
      end
   end

   pathname = ospath(pathname, OS$FULLPATH)

   * Check if account directory exists
   * rev 0.9.0 we no longer support conversion of existing directoris to sd accounts
   * too many unknows with whether this is a non sd type account (Scarlett or other)

   if ospath(pathname, OS$EXISTS) then
      @system.return.code = -ER$NOT.CREATED
      stop sysmsg(10009) ;* 'Cannot Create a New SD Account in an Existing Directory'
   end else
   * create the new account directory  
      create.file pathname directory on error
         @system.return.code = -ER$NOT.CREATED
         stop sysmsg(6013) ;* Unable to create account directory
      end
      
* rev 0.9.0 *
* for all accounts set the setgid bit
* When the bit is set for a directory, the set of files in that directory will have the same group as the group of the parent directory,
* and not that of the user who created those files.
* This is used for file sharing since they can be now modified by all the users who are part of the group of the parent directory.
*
      if upcase(acc.type) = "GROUP"  or  upcase(acc.type) = "USER" then
        OS.EXECUTE "sudo chmod g+s ":pathname 
        stat = OS.ERROR()
        if stat = 0 then
          crt sysmsg(10010,pathname)  ;* setgid bit set on: %1 
        end else
          crt sysmsg(10011,stat) ;* 'Warning unable to setgid bit on Group Folder, status: %1
        end
      end

      
     * rev 0.9.0  create group for the account *   
      begin case
      case upcase(acc.type) = "USER" 

     *  is this user a member or sdusers?
        if not(is_grp_member(acc.uname,'sdusers')) then
         * not a member, add them
          os.execute "sudo usermod -aG sdusers ":acc.uname
          stat = OS.ERROR()
          if stat = 0 then
            crt sysmsg(10013,acc.uname)   ;* %1 added to sdusers'
          end else
            @system.return.code = -ER$FAILED
            stop sysmsg(10012,acc.uname,stat) ;* Unable to add user %1 to sdusers, status: %2
          end
        end 
      
       
      *
      * create user's sd group      
        group_name = "sdu_":acc.uname      
        gosub create.group  
        if not(group_success) then
          stop
        end
		
      case upcase(acc.type) = "GROUP" 
      *
      * create account's sd group 
* rev 1.0-1	  
        group_name = downcase("sdg_" : acc.name) ;* callers entered group name, force lower case with sdg_prefix
        gosub create.group
        if not(group_success) then
          stop
        end     
      end case
      
      *      
*     make the accountfolders and copy files    
*     Note --> if there is a failure in make.account the program terminates
*     We could end up with groups created and no acctual account
*
      gosub make.account
      

 * 20240225 mab set file ownership for account parent dir
      set.owner.path = pathname
      gosub set.owner

   end

   * Add ACCOUNTS entry

   display sysmsg(6014) ;* Adding to register of accounts...

   acc.rec = ''
   acc.rec<ACC$PATH> = pathname
   * rev 0.9.0 add group name to ACCOUNT record
   * group name depends on which type of ACCOUNT we are creating
   * We do not default to ACCOUNT name because account names are (historically?) always upper case
   * Not sure what to do about account type "OTHER" ???
   begin case
   case upcase(acc.type) = "USER"
     acc.rec<ACC$GROUP> = "sdu_":acc.uname
   case upcase(acc.type) = "GROUP"
     acc.rec<ACC$GROUP> = group_name
   end case  
   write acc.rec to acc.f, acc.name
   close acc.f

   @system.return.code = 0
   return

* ======================================================================

make.account:
   * Set up VOC

   display sysmsg(6015) ;* Creating VOC...
* rev 0.9-3 Nov 25 move voc back to dynamic file
   create.file pathname:@ds:'VOC' dynamic on error
      @system.return.code = -6
      stop sysmsg(6016) ;* Unable to create VOC
   end
   set.owner.path = pathname:@ds:'VOC'
   gosub set.owner
   set.owner.path = pathname:@ds:'VOC':@ds:'%0'
   gosub set.owner
   set.owner.path = pathname:@ds:'VOC':@ds:'%1'
   gosub set.owner

   openpath pathname:@ds:'VOC' to voc.f else
      @system.return.code = -status()
      stop sysmsg(6017) ;* Unable to open new VOC
   end

   openpath @sdsys:@ds:"NEWVOC" to newvoc.f else
      @system.return.code = -status()
      stop sysmsg(6018) ;* Cannot open SDSYS NEWVOC
   end

   select newvoc.f to 11
   loop
      readnext id from 11 else exit

      read rec from newvoc.f, id then
         rec<1> = if upcase(rec[1,1]) = 'P' then rec<1>[1,2] else rec[1,1]

         if upcase(rec<1>) = 'K' and rec<3> # '' then   ;* Keyword with other use
            s = rec<3>
            rec<3> = if upcase(s[1,1]) = 'P' then s<1>[1,2] else s[1,1]
         end

         write rec to voc.f, id
       
      end
   repeat

   close newvoc.f

   * Set up $COMMAND,STACK VOC entry

   write 'X' to voc.f, '$COMMAND.STACK'

   * Create $HOLD

   fn = '$HOLD'
   os.name = '$HOLD'
   descr = 'File for deferred prints'
   dict = @true
   gosub create.dir.file


   * Create $SAVEDLISTS

   fn = '$SAVEDLISTS'
   os.name = '$SVLISTS'
   descr = 'File for saved select lists'
   dict = @false
   gosub create.dir.file

* 20240308 mab add BP when creating new account
  * Create BP

   fn = 'BP'
   os.name = 'BP'
   descr = 'File for BASIC programs'
   dict = @false
   gosub create.dir.file


   * Create private catalogue

   display sysmsg(6019) ;* Creating private catalogue directory..."

   s = pathname:@ds:'cat'
   if not(ospath(s, OS$EXISTS)) then
      create.file s directory on error
         @system.return.code = -ER$NOT.CREATED
         stop sysmsg(6020) ;* Unable to create private catalogue directory
      end

 * 20240225 mab set file ownership for cat
      set.owner.path = pathname:@ds:'cat'
      gosub set.owner
   end

 * 20240225 mab set file ownership for VOC
   set.owner.path = pathname:@ds:'VOC'
   gosub set.owner


   return
   
* ======================================================================
   * Check for further command arguments
more.args:
   loop
      call @parser(parser$get.token, token.type, token, keyword)
   until token.type = PARSER$END
      begin case
         case keyword = KW$NO.QUERY
            no.query = @true

         case 1
            stop sysmsg(2018, token) ;* Unexpected token (xx)
      end case
   repeat   
return
* ======================================================================

create.dir.file:
   display sysmsg(6021, fn) ;* Creating xx...

   create.file pathname:@ds:os.name directory on error
      @system.return.code = -ER$NOT.CREATED
      stop sysmsg(6022, fn) ;* Unable to create data part of xx
   end
* 20240225 mab set file ownership accordingly  
   set.owner.path = pathname:@ds:os.name
   gosub set.owner

   begin case
      case descr[1,1]= 'F'       ; voc.rec = descr
      case descr # ''            ; voc.rec = 'F ' : descr
      case 1                     ; voc.rec = 'F'
   end case

   voc.rec<2> = os.name

   if dict then
      create.file pathname:@ds:os.name:'.DIC' dynamic on error
         @system.return.code = -ER$NOT.CREATED
         stop sysmsg(6023, fn) ;* Unable to create dictionary part of xx
      end
* 20240302 mab set file ownership accordingly 
* rev 0.9.0 * 
      set.owner.path = pathname:@ds:os.name:'.DIC':@ds:'%0'
      gosub set.owner
      set.owner.path = pathname:@ds:os.name:'.DIC':@ds:'%1'
      gosub set.owner            

      openpath pathname:@ds:os.name:'.DIC' to dict.f else
         @system.return.code = -status()
         stop sysmsg(6024, fn) ;* Unable to open dictionary part of xx
      end
* 20240121 mab use @VM not hard coded hex fd 
      write raise('D':@VM:'0':@VM:@VM:fn:@VM:'10L':@VM:'S') to dict.f, '@ID'
      close dict.f

* 20240225 mab set file ownership accordingly  
      set.owner.path = pathname:@ds:os.name:'.DIC'
      gosub set.owner

      voc.rec<3> = os.name:'.DIC'
   end

   write voc.rec to voc.f, fn

   return


* ======================================================================
* 20240225 mab set file ownership accordingly   
* assumes:
* set.owner.path - file path
* acc.type - USER/GROUP/OTHER
* acc.name - populated
set.owner:

   begin case
      case upcase(acc.type) = 'USER'
        null ;* uid/gid set at "case upcase(acc.type) = 'USER'" validation
* rev 0.9.0 *        
      case upcase(acc.type) = 'GROUP' 
        uid='sdsys'
        gid= group_name
      case 1
        uid='root'
        gid='sdusers'
   end case
   chown_parm = uid:@VM:gid:@VM:set.owner.path
   * crt 'set.owner: ':chown_parm
   result = ospath(chown_parm,OS$CHOWN)
   if result <= 0 then
     st = set.owner.path:' err: ':status()
     display sysmsg(10000,st) ;* Unable change ownership of directory error %1 
   end

return

* rev 0.9.0 
*=======================================================================
* create.group
* group_name - name of group to create
* acc.type   - account type (USER/GROUP)
* acc.uname  - user name for acc.type USER
* return group_success t/f
*        group_stat
* rem: stat groupadd
* 0 success
* 2 invalid command syntax
* 3 invalid argument to option
* 4 GID not unique (when -o not used)
* 9 group name not unique
* 10 can't update group file
create.group:  *
 group_success = @TRUE 
*
* does group already exist in /etc/group?
*      
   if not(is_group(group_name)) then 
*
* no, attempt to create it
*
      if upcase(acc.type) = 'USER' then
        OS.EXECUTE "sudo groupadd -U root,sdsys,":acc.uname :" ": group_name
      end else
        OS.EXECUTE "sudo groupadd -U root,sdsys ": group_name
      end
      group_stat = OS.ERROR()
      if group_stat = 0 then
        crt sysmsg(10014,group_name)   ;* Group -  %1 created
      end else
        crt sysmsg(10015,group_name,group_stat)   ;* Unable to create group: %1 status: %2
        group_success = @FALSE
      end
   end
return


end

* END-CODE
